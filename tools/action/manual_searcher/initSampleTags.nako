※【なでしこ実行モード】cnako
CNAKOモードは『CON』

！`lib\sqlite.nako`を取り込む
！`constants.nako`を取り込む

！対象ファイル名とは文字列=タグDBファイルのファイル名抽出

もし使用命令データが存在するんでなければ
　使用命令初期化を起動待機

"
# initSampleTags.nako ----------------
# 検索用データベースの構築開始
# > {対象ファイル名}
"と表示

`SJIS`にSQLITE出力コード設定。

雛型とはハッシュ

もしタグDBファイルが存在するならば
　タグDBファイルをファイル削除

TAGDBとはSQLITE
これについて
　ファイルはタグDBファイル
　テーブルは`sample`
　もしタグDBファイルのファイルサイズが0KBならば
　　ヘッダをカンマで配列結合
　　それでテーブル作成　　

●タグ登録({文字列}パスを{文字列}名前として{文字列}タグで)
　ARRとは配列。
　パスを表示
　タグ=タグのカンマを改行に置き換え
　TAGDBで「SELECT {ヘッダ[MYID]},{ヘッダ[TAG]} FROM sample WHERE {ヘッダ[PATH]}="{パス}";」を実行
　それをCSV取得。ARR=_[1]
　雛型@(ヘッダ[NAME])=名前
　雛型@(ヘッダ[PATH])=パス
　もしARRの要素数が0ならば
　　雛型@(ヘッダ[TAG ])=タグ
　　雛型@(ヘッダ[MYID])=IDNUMに1を直接足す
　　雛型をTAGDBへレコード挿入
　違えば
　　タグに改行&ARR[1]を追加
　　雛型@(ヘッダ[TAG ])=タグ
　　雛型@(ヘッダ[MYID])=ARR[0]
　　TAGDBで「{ヘッダ[MYID]}={ARR[0]};」をデータ削除
　　雛型をTAGDBへレコード挿入

IDNUMとは整数=0

#-----------------------------------------------------------------------
雛型@ヘッダ[TYPE]=OFFLINE

#-----------------------------------------------------------------------
# usedCommands.dat

もし使用命令データが存在するならば
　"{~}<-- Commands used in samples{~}"と表示
　使用命令データを読んでナデシコする
　"{~}    Commands used in samples -->"と表示

# usedCommands.dat
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# userTags.dat

もしユーザタグが存在するならば
　"{~}<-- User tags{~}"と表示
　ユーザタグを読んでナデシコする
　"{~}    User tags -->"と表示

# userTags.dat
#-----------------------------------------------------------------------

"
# > {対象ファイル名} ({タグDBファイルのファイルサイズ}KB)
# ... データベース構築完了
"と表示

終わる
