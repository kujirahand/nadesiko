/*
[メールの送信]
以下の項目を設定するとメールが送信できます。
(*) Gmail/Yahoo!メール対応しました!
---
メール差出人は『xxx@example.com』
メール宛先は『xxx@example.com』
メール件名は『メールのテストです』
メール本文は『ここに本文を書きます。』
『aaaa@gmail.com』の『password』でGMAIL送信。
*/
#-----------------------------------------------------------------------
!"action.nako"を取り込む。
#-----------------------------------------------------------------------
# GUI
母艦は「メールの送信」
母艦タイトルは母艦のテキスト
母艦のW=340
母艦のH=300
母艦を中央移動。
#-----------------------------------------------------------------------
「どの方法を利用しますか？」を、
「Gmail
Yahoo!メール
Yahoo!BBメール
その他」でボタン選択して、Aに代入。
もし、A=「」ならば、終わる。
もし、A=「Gmail」ならば
　　GMAIL処理。終わる。
違えば、もし、A=「Yahoo!メール」ならば
　　YAHOO処理。終わる。
違えば、もし、A=「Yahoo!BBメール」ならば
　　YAHOOBB処理。終わる。

＃その他の場合
「メール認証(Auth-SMTP)が必要ですか？
通常は不要です。」と二択。
もし、それが空ならば終わり。

もし、それがはいならば
　　認証必要処理
違えば
　　認証不要処理
ここまで
終わり。

●認証必要処理
　　「ホスト=mail.xxx.jp
差出人=xxx@xxx.jp
宛先=xxx@xxx.jp
件名=?s
ID=?e
パスワード=?e
オプション=?(APOP|SASL|SSL)」と項目記入。
　　もし、それが空ならば、終わり
　　Ｆはそれ。
    「本文をここに記入」でメモ記入
    「『」を『「』に置換。
    「』」を『」』に置換。
    本文はそれ。
    #---挿入するプログラム
　　「
# サーバーの設定
メールホストは『{F@"ホスト"}』
メールIDは、『{F@"ID"}』
メールパスワードは『{F@"パスワード"}』
メールオプションは『{F@"オプション"}』

# メールの内容について
メール差出人は『{F@"差出人"}』
メール宛先は『{F@"宛先"}』
メール件名は『{F@"件名"}』

# 本文について
メール本文は『{本文}』

# 実際の送信処理
メール送信。
『送信完了』と表示。」をエディタ挿入。

●認証不要処理
　　「ホスト=mail.xxx.jp
差出人=xxx@xxx.jp
宛先=xxx@xxx.jp
件名=?s」と項目記入。
　　もし、それが空ならば、終わり
　　Ｆはそれ。
    「本文をここに記入」でメモ記入
    それを言う
    「『」を『「』に置換。
    「』」を『」』に置換。
    本文はそれ。
    #---挿入するプログラム
　　「
# サーバーの設定
メールホストは『{F@"ホスト"}』

# メールの内容について
メール差出人は『{F@"差出人"}』
メール宛先は『{F@"宛先"}』
メール件名は『{F@"件名"}』

# 本文について
メール本文は『{本文}』

# 実際の送信処理
メール送信。
『送信完了』と表示。」をエディタ挿入。

！共通テンプレート＝「#--- メール送信処理 ---
メール差出人は『xxx@example.com』
メール宛先は『xxx@example.com』
メール件名は『メールのテストです』
メール本文は『
ここに本文を書きます。
*******
*******
何行書いてもOKです。
』
」

●GMAIL処理
　　「アカウント=
パスワード=」と項目記入してAに代入。
　　もし、Aが空ならば、終わる。
　　「{共通テンプレート}『{A@"アカウント"}』の『{A@"パスワード"}』でGMAIL送信」をエディタ挿入。

●YAHOO処理
　　「アカウント=
パスワード=」と項目記入してAに代入。
　　もし、Aが空ならば、終わる。
　　「{共通テンプレート}『{A@"アカウント"}』の『{A@"パスワード"}』でYAHOOメール送信」をエディタ挿入。

●YAHOOBB処理
　　「アカウント=
パスワード=」と項目記入してAに代入。
　　もし、Aが空ならば、終わる。
　　「{共通テンプレート}『{A@"アカウント"}』の『{A@"パスワード"}』でYAHOOBBメール送信」をエディタ挿入。

