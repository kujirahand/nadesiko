/*
[メニューエディタ]
ウィンドウ上部にあるメニューを一括作成するためのメニューです。
*/
!"action.nako"を取り込む。

「
なし,ファイルメニュー,ファイル(&F),,
   -,新規メニュー,    新規(&N),Ctrl+N,,新規処理
   -,-
   -,開くメニュー,    開く(&O),Ctrl+O,,開く処理
   -,保存メニュー,    保存(&S),Ctrl+S,,保存処理
   -,-
   -,閉じるメニュー,閉じる(&C),Ctrl+Shift+F4,,終わる
なし,出力メニュー,出力(&O),,
   -,テストメニュー,テスト(&T),F5,,テスト出力処理
   -,エディタ出力メニュー,エディタへ貼り付け(&V),,,エディタ出力処理
   -,内容確認メニュー,内容を確認,,,出力処理
なし,ヘルプメニュー,ヘルプ(&H),
   -,ヘルプ1メニュー,ヘルプ(&H),F1,,ヘルプ処理。
」をメニュー一括作成。

母艦は「メニューエディタ」
メイングリッドとはグリッド。
そのレイアウトは「全体」
そのアイテムは「#親部品名,部品名,テキスト,ショートカット,オプション,イベント
なし,ファイルメニュー,ファイル(&F),
-,開くメニュー,開く(&O),Ctrl+O,
-,-
-,閉じるメニュー,閉じる(&C),Ctrl+Shift+F4,,終わる」
その編集はオン。
その行は２
その列は１
そのキータイピング時は〜
　　もし(メイングリッドの押されたキー＝CHR(13))ならば
　　　　S=メイングリッドのアイテム。
        もし(メイングリッドの行≧(Sの要素数-1))ならば
        　　S=S＆改行
            メイングリッドのアイテムはS

●グリッドデータ取得
　　S=メイングリッドのアイテム
    S=「『{S}』をメニュー一括作成。」
    それはS

●出力処理
    グリッドデータ取得して言う。

●エディタ出力処理
　　S=グリッドデータ取得
    Sをエディタ挿入。

●テスト出力処理
　　S=グリッドデータ取得。
    TMP=テンポラリフォルダ＆「test.nako」
    Sを、TMPへ保存。
    「"{ランタイムパス}vnako.exe" "{TMP}"」を起動。

●新規処理
　　メイングリッドのアイテムは「#親部品名,部品名,テキスト,ショートカット,オプション,イベント
,,,
,,,」

●開く処理
　　「.csv」のファイル選択。
    もし、それが空ならば、戻る。
    それを開く。
    メイングリッドのアイテムはそれ。

●保存処理
　　F=「.csv」の保存ファイル選択
    もし、Fが空ならば、戻る。
    メイングリッドのアイテムを、Fへ保存。
    BEEP。

●ヘルプ処理
　　「メニューを作ります。
------------------
メニューには、親と子があります。
親となるメニューは、親部品命を空欄にしてください。

もし、直前のメニューが親部品となるときは、部品名を"-"にすると、
直前のメニューが親とみなされます。

部品名に、"-"をつけると、区切り線になります。

親部品名を"#"から始めるとコメントの意味になります。」を言う。
