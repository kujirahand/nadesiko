/*
[多角形描画ツール]
多角形を描画するプログラムを生成できます。
*/
#-----------------------------------------------------------------------
# 多角形描画
#-----------------------------------------------------------------------
!"action.nako"を取り込む
母艦は「多角形描画プログラム」

「,m1,図形
m1,m2,初期化,,,初期化処理
m1,m3,描画プログラムをコピー,,,描画プログラムコピー処理
,m4,塗り
m4,m5,塗り色変更,,,色変更処理
,m6,編集
m6,m7,一図形戻す,,,UNDO処理
,m8,出力
m8,m9,プログラムをエディタに貼り付け,,,エディタ貼り付け処理
m8,mA,プログラムをコピー,,,描画プログラムコピー処理」をメニュー一括作成

初期化処理。
座標データは「」
描画ＰＧは「」
Ｆ描画ＰＧは「」＃ＵＮＤＯ用バッファ
選択色は青色。
前選択色は「」。

最後X=-1
最後Y=-1
母艦のマウス押した時は〜
    ＃目印をつける
    母艦について
    　　XX = マウスX
        YY = マウスY
    ＃パスを閉じるか調べる
    もし、（座標検索(XX,YY) >= 0)ならば
    　　塗り色は選択色。
        座標データへ多角形。
        最後Ｘ＝−１
        最後Ｙ＝−１
        ＃「パスを閉じ、座標をコピーしました。｛改行｝｛座標データ｝」と言う。
        ＃色
        Ｆ描画ＰＧ＝描画ＰＧ。＃一手戻す用にバックアップ
        もし、選択色が前選択色でなければ
        　　前選択色は選択色。
            描画ＰＧ＝描画ＰＧ＆「塗り色は{選択色}{改行}」
        ＃座標
        座標データをトリムして、
        改行を「,」に置換して座標データに代入。
        描画ＰＧ＝描画ＰＧ＆「{[}{座標データ}{]}で多角形。{改行}」
        座標データ＝「」
        ＃描画しなおす
        画面初期化。
        描画ＰＧでナデシコする
        戻る
    塗り色は白色
    線色は黒色
    線太さは１
    円(XX-4,YY-4,XX+4,YY+4)
    ＃前からの点があれば結ぶ
    もし、最後Ｘ＞0ならば
    　　線色はRGB(200,200,200)
        線太さは1
        線(最後X,最後Y,XX,YY)
　　ここまで
    最後X=XX
    最後Y=YY
    ＃パスを追加
    座標データは、座標データ＆「{XX},{YY}」＆改行

●座標検索(X,Y)
　　ＸＸとは整数
    ＹＹとは整数
    結果=-1
    IDX=0
    座標データを反復
    　　それを「,」で区切る
        XX = _[0]
        YY = _[1]
        もし、((XX-4)<=X)&&(X<=(XX+4))&&((YY-4)<=Y)&&(Y<=(YY+4))ならば
        　　結果＝IDX
            抜ける
        ここまで
        IDX=IDX+1
    それは結果


●画面初期化
　　画面クリア。
    # 座標の目安を描画
　　W = 64
　　線太さは１
　　W_CNT = INT(母艦のクライアントＷ / W)
　　Xを1からW_CNTまで繰り返し
　　　　もし、(X-1)%2 = 0ならば
        　　線色は、RGB(220,220,220)
        違えば
        　　線色は、RGB(180,180,180)
        LINE(X*W,0,X*W,母艦のクライアントＨ)
    H_CNT = INT(母艦のクライアントＨ / W)
　　Yを1からH_CNTまで繰り返し
　　　　もし、(Y-1)%2 = 0ならば
        　　線色は、RGB(220,220,220)
        違えば
        　　線色は、RGB(180,180,180)
　　　　LINE(0,Y*W,母艦のクライアントＷ,Y*W)

●初期化処理
    # 初期化
    画面初期化
    座標データは「」
    描画ＰＧは「」
　　前選択色は「」。

●描画プログラムコピー処理
　　もし、座標データが空ならば
    　　Ｓは、描画ＰＧ
    違えば
    　　Ｓは、「{描画ＰＧ}{改行}{[}{座標データ}{]}で多角形。」
　　Ｓをコピー。
    「以下のプログラムをコピーしました。{改行}{S}」を言う。

●エディタ貼り付け処理
　　もし、座標データが空ならば
    　　Ｓは、描画ＰＧ
    違えば
    　　Ｓは、「{描画ＰＧ}{改行}{[}{座標データ}{]}で多角形。」
　　ここまで
    Sをエディタ挿入。
    

●色変更処理
　　色選択
    もし、それが空ならば戻る
    選択色はそれ。

●UNDO処理
　　初期化処理。
    Ｆ描画ＰＧをナデシコする。
    描画ＰＧはＦ描画ＰＧ
