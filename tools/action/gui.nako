/*
[GUIフォームエディタ]
簡単なフォームデザイナです。
画面デザインを作るのに使えます。
*/
#-----------------------------------------------------------------------
#-----------------------------------------------------------------------
# GUI フォームエディタ
#-----------------------------------------------------------------------
＃!「action.nako」を取り込む
#-----------------------------------------------------------------------
母艦のタイトルは「GUI フォームエディタ」
#-----------------------------------------------------------------------
カーソルとはトラック。
部品個数は０
部品配列とは配列
選択部品とはグループ
選択部品に母艦をグループ参照コピー
グリッドサイズは8
グリッド描画処理。
#-----------------------------------------------------------------------
「
,m01,ファイル(&F)
m01,mL2,-
m01,m04,終わり,,,終了処理
,m05,部品作成(&B)
m05,m06,ボタン  ,,,『ボタン』を部品作成処理
m05,m07,エディタ,,,『エディタ』を部品作成処理
m05,m08,メモ    ,,,『メモ』を部品作成処理
m05,m09,コンボ  ,,,『コンボ』を部品作成処理
m05,m10,リスト  ,,,『リスト』を部品作成処理
m05,m11,ラベル  ,,,『ラベル』を部品作成処理
m05,m12,イメージ,,,『イメージ』を部品作成処理
,m20,出力(&O)
m20,m21,完成データをコピー(&P),,,完成データコピー処理
,m30,オプション(&P)
m30,mGr,グリッドに合わせる,,,グリッドメニュー処理
」をメニュー一括作成
mGr→チェック＝オン
#-----------------------------------------------------------------------
#-----------------------------------------------------------------------
●ファイル新規処理
　　「未実装です」と言う。

●ファイル開く処理
　　「未実装です」と言う

●ファイル保存処理
　　「未実装です」と言う

●終了処理
　　「終わりますか？」と二択。
    もし、それがはいならば終わり。

●部品作成処理（Ｂを）
    部品配列[部品個数]をイメージとして作成。
    ＃
    部品配列[部品個数]→Ｘ＝カーソル→Ｘ
　　部品配列[部品個数]→Ｙ＝カーソル→Ｙ
　　部品配列[部品個数]→Ｗ＝カーソル→Ｗ
　　部品配列[部品個数]→Ｈ＝カーソル→Ｈ
    部品配列[部品個数]→クリックした時は〜部品選択処理
    部品配列[部品個数]→ポケット＝Ｂ
    部品配列[部品個数]→タグ＝部品個数
    ＃選択部品に部品配列[部品個数]をグループ参照コピー。
    選択部品に母艦をグループ参照コピー。
    部品配列[部品個数]の部品描画処理
    部品個数＝部品個数＋１
　　「tools\basi.wav」をWAV再生


●完成データ生成
　　結果とは文字列
    Ｉとは整数

　　それは「」
　　もし、部品個数≦０ならば、戻る。
    結果＝結果＆「母艦のW={母艦のW}{~}母艦のH={母艦のH}{~}」

    Ｉを０から部品個数-1まで繰り返す
    　　部品種類＝部品配列[Ｉ]→ポケット
        XX＝部品配列[Ｉ]→Ｘ
        YY＝部品配列[Ｉ]→Ｙ
        WW＝部品配列[Ｉ]→Ｗ
        HH＝部品配列[Ｉ]→Ｈ
        結果＝結果＆「#---部品
部品{I}とは{部品種類}。
そのX={XX}。そのY={YY}。そのW={WW}。そのH={HH}
」
　　ここまで
    それは結果。

●完成データコピー処理
　　Ｓ＝完成データ生成。
    Ｓをコピー。
    Ｓを言う。

●部品描画処理（{グループ}Gの）
    ＃塗り色の指定
    塗りスタイルは「べた」
    塗り色はウィンドウ背景色
    線色は黄色
    線太さは３
    文字サイズは９
    ＃描画
    Ｇ→画像は「」
    Ｇ→四角（0,0,Ｇ→W,Ｇ→H)
    Ｇ→文字描画(0,0,「部品{Ｇ→タグ}:{Ｇ→ポケット}」)

●選択部品レポート
    Gに選択部品をグループ参照コピー。
    母艦＝「部品{G→タグ}:{G→ポケット}:{G→W},{G→H}」


●部品選択処理
　　イベント部品を選択部品にグループ参照コピー。
    カーソルのＸ＝選択部品→Ｘ
    カーソルのＹ＝選択部品→Ｙ
    カーソルのＷ＝選択部品→Ｗ
    カーソルのＨ＝選択部品→Ｈ
    「tools\ki.wav」をWAV再生
    Gに選択部品をグループ参照コピー。
    母艦＝「部品{G→タグ}:{G→ポケット}:{G→W},{G→H}」

●グリッドメニュー処理
    もし、グリッドサイズ≧１ならば
    　　グリッドサイズは-1
        mGr→チェック＝オフ
    違えば
    　　グリッドサイズは8
        mGr→チェック＝オン
    ここまで
    グリッド描画処理。
    BEEP
    

母艦のマウス押した時は〜
　　母艦を選択部品にグループ参照コピー。
    カーソルのＸは母艦のマウスＸ
    カーソルのＹは母艦のマウスＹ

#-----------------------------------------------------------------------

カーソルのマウス離した時は〜
　　カーソルのサイズ変更した時

カーソルのサイズ変更した時は〜
　　Ｓ＝選択部品→名前
　　もし、Ｓ＝「母艦」ならば、戻る。
    
    XX=カーソルのＸ
    YY=カーソルのＹ
    WW=カーソルのＷ
    HH=カーソルのＨ
    
    もし、グリッドサイズ＞０ならば
    　　XX=INT(XX÷グリッドサイズ+0.5) * グリッドサイズ
    　　YY=INT(YY÷グリッドサイズ+0.5) * グリッドサイズ
    　　HH=INT(HH÷グリッドサイズ+0.5) * グリッドサイズ
    　　WW=INT(WW÷グリッドサイズ+0.5) * グリッドサイズ
    
    選択部品→Ｘ＝XX
    選択部品→Ｙ＝YY
    選択部品→Ｗ＝WW
    選択部品→Ｈ＝HH
    選択部品の部品描画処理



●グリッド描画処理
　　ウィンドウ背景色で画面クリア
    もし、グリッドサイズ≦１ならば戻る。
    ＃点を描画
    ＣＸ＝INT(母艦のＷ÷グリッドサイズ）
    ＣＹ＝INT(母艦のＨ÷グリッドサイズ)
    ＩＸを０からＣＸまで繰り返す
    　　ＩＹを０からＣＹまで繰り返す
        　　ＸＸ＝ＩＸ＊グリッドサイズ
            ＹＹ＝ＩＹ＊グリッドサイズ
            線太さ＝１
            線色は黒色
            線(XX,YY,XX+1,YY+1)


母艦のサイズ変更した時は〜
　　母艦の画像は「」
    グリッド描画処理。
    
    