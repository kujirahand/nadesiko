#===========================================================
# スルーテスト
#===========================================================
# テスト実行メイン
#※「###テスト省略###」と書かれたファイルは省略する
#
「なでしこテスト:{ナデシコバージョン}
ランタイム：{ナデシコランタイムパス}
」を表示。

テストフォルダ＝「{ランタイムパス}test\」
FDIR＝「」
「{テストフォルダ}*.nako」の全ファイル列挙。
配列ソート。
反復
　　FFFテストファイル＝対象
　　Ｐ＝対象からパス抽出
　　もし、Ｐ＝テストフォルダならば、続ける。
　　もし、FDIR≠Pならば
　　　　FDIR=P
　　　　FDIRを表示。
　　Ｐ＝対象からファイル名抽出
　　もし、Ｐ＝「temp.nako」ならば、続ける。
　　母艦のタイトル＝「[読]」&FFFテストファイル
　　FFFテストファイルを開く。
　　S=それ。
　　NN=Sで「###テスト省略###」が何文字目
　　もし(NN>0)ならば、続ける
　　Ｎ＝Ｓの関数列挙処理。
　　Ｓをナデシコする。
　　Ｎを反復
　　　　FFF関数名＝対象。
　　　　母艦のタイトル＝「{FFF関数名}:{P}」
　　　　FFF関数名をナデシコする。
#===========================================================
# 結果表示
Ｔ＝テスト結果取得
ログ＝テストログ取得。
ログ＝ログの0から"NG"を表ピックアップ
結果＝「失敗」
N_ALL= T@"all"
N_OK = T@"ok"
N_NG = T@"ng"
N_PER = INT(N_OK/N_ALL*100)
もし（Ｔ＠「ng」＝0）ならば、結果＝「成功」
ＭＳＧ＝「■テスト結果の報告
---
なでしこ:{ナデシコバージョン}({ナデシコ最終更新日})
OS:{OS} → {OSバージョン}
---
テスト結果:{結果}
テスト成功:{N_OK}/{N_ALL} → {N_PER}%
テスト失敗:{N_NG}
---
{ログ}」
もし(結果＝「成功」)ならば
　　ＭＳＧを言う。
　　「テスト結果をサーバーに記録して宜しいですか？」と二択。
　　もし、それ＝はいならば
　　　　URL=「http://nadesi.com/cgi/nadesiko/testok/ok.php」
　　　　クエリ＝「ver={ナデシコバージョン}&os={OS}」
　　　　Ｒ＝「{URL}?{クエリ}」からHTTPデータ取得。
　　　　「ありがとうございました。｛Ｒ｝｛改行｝結果を見ますか？」と二択。
　　　　もし、それがはいならば
　　　　　　「{URL}?mode=show」を起動。
違えば
　　「■テストに失敗しました。」&改行&
　　「失敗した旨をメールかバグ報告の板に記入してください。」＆改行＆
　　「---------------------{改行}」＆改行＆
　　ＭＳＧを、メモ記入。
終わる。

/*
●テスト実行(AとBで)
　　FFFテスト数＝FFFテスト数＋１
　　もし（Ａ＝Ｂ）ならば
　　　　FFFテスト成功＝FFFテスト成功＋１
　　違えば
　　　　FFFテスト失敗＝FFFテスト失敗＋１
　　　　Ａ＝Ａの改行を「\n」に置換
　　　　Ｂ＝Ｂの改行を「\n」に置換
　　　　FFFテストログ＝FFFテストログ＆「失敗：●{FFF関数名}←{FFFテストファイル}{改行}」
　　　　FFFテストログ＝FFFテストログ＆「　　　『{A}』≠『{B}』」＆改行
*/

●関数列挙処理（Ｓの）
　　結果とは変数
　　Ｆとは変数
　　結果＝空
　　Ｓを反復
　　　　Ｆ＝MID(対象,1,4)
　　　　もし(Ｆ = "●テスト")ならば
　　　　　　結果＝結果＆MID(対象,2,999)＆改行
　　それは結果。
#===========================================================


