#-----------------------------------------------------------------------
# ツリーサンプル（動物の分類）
#-----------------------------------------------------------------------
# ツリーの作成
動物ツリーとはツリー。
# 設定
動物ツリーの文字サイズは、24
動物ツリーのレイアウトは「全体」
# アイテムを設定する
# "親ID, ID, テキスト, 画像番号, 選択画像番号" を１行ずつ書く
動物ツリーのアイテムは、「,動物,動物
動物,哺乳類,哺乳類
哺乳類,サル,サル
哺乳類,イヌ,イヌ
哺乳類,ウシ,ウシ
動物,爬虫類,爬虫類
爬虫類,ヘビ,ヘビ
爬虫類,カメ,カメ」
# ツリーのノードを開く場合
「動物」を動物ツリーでノード開く。
#-----------------------------------------------------------------------
# メニュー
「,mnuFile,ファイル(&F)
mnuFile,mnuOpen,テキストで読込,,,開く処理
mnuFile,mnuSave,テキストで保存,,,保存処理
,mnuAdd,追加(&A)
mnuAdd,mnuAddNext,このノードに追加,,,次ノード追加処理
mnuAdd,mnuAddNode,子ノードに追加,,,子ノード追加処理
,mnuDel,削除(&D)
mnuDel,mnuDelNode,ノード削除,,,ノード削除処理
,mnuEdit,修正(&E)
mnuEdit,mnuEditText,テキスト修正,,,テキスト修正処理」をメニュー一括作成
#-----------------------------------------------------------------------
# ポップアップ
ツリーポップとはポップアップメニュー
「,popAddChild,子に追加,,,子ノード追加処理
,popDelNode,削除,,,ノード削除処理
,popEdit,編集,,,テキスト修正処理
,popline,-
,popPath,パス取得,,,パス取得処理」をツリーポップに一括追加
動物ツリーのポップアップメニューはツリーポップ。

●子ノード追加処理
　　選択IDは、動物ツリーの選択ID
    もし、選択ID=「」ならば
    　　「ツリーで追加したい場所を選択してください」と言う
        戻る
    「動物の名前は？」と尋ねる。
    もし、それが空ならば、戻る。
    入力動物はそれ。
    FID＝入力動物を動物ツリーのノード番号取得。
    もし、FID>=0ならば
    　　「｛入力動物｝は既に入力されています」と言う
    違えば
    　　「{選択ID},{入力動物},{入力動物}」を動物ツリーに追加。
    ここまで

●次ノード追加処理
　　選択IDは、動物ツリーの選択ID
    もし、選択ID=「」ならば
    　　「ツリーで追加したい場所を選択してください」と言う
        戻る
    「動物の名前は？」と尋ねる。
　　もし、それが空ならば、戻る。
    入力動物はそれ。
    FID＝入力動物を動物ツリーのノード番号取得。
    もし、FID>=0ならば
    　　「｛入力動物｝は既に入力されています」と言う
        戻る
    親IDは、選択IDで動物ツリーの親ノード取得。
    「{親ID},{入力動物},{入力動物}」を動物ツリーに追加。
    
●ノード削除処理
　　選択IDは、動物ツリーの選択ID
    もし、選択ID=「」ならば
    　　「ツリーで削除したい場所を選択してください」と言う
        戻る
    もし、選択ID＝「動物」ならば
    　　「今回、動物は削除できません。」と言う
        戻る
    ＃削除
    選択IDを、動物ツリーからノード削除。

●テキスト修正処理
　　選択IDは、動物ツリーの選択ID
    もし、選択ID=「」ならば
    　　「ツリーで修正したい場所を選択してください」と言う
        戻る
    「修正するテキストを入力してください」と尋ねる。
    もし、それが空ならば、戻る
    修正はそれ。
    選択IDを修正に動物ツリーのテキスト変更

●開く処理
　　「.txt」のファイル選択。
    もし、それが空ならば終わり。
    ファイル名はそれ。
    ファイル名をデータへ開く。
    動物ツリーのアイテムは、データ。

●保存処理
　　「.txt」の保存ファイル選択。
    もし、それが空ならば終わり。
    ファイル名はそれ。
    母艦は「保存名：｛ファイル名｝」
    動物ツリーのアイテムをファイル名へ保存。
    「保存しました」と言う

●パス取得処理
　　パス=動物ツリーの選択パス取得
    もし、パスが空ならば
    　　「ツリーを選択してください。」と言う
    違えば
    　　パスを言う
    