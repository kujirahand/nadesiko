#-----------------------------------------------------------------------
# ファイルの分割ツールを作ってみよう！（第75回） 
# http://pc.nikkeibp.co.jp/article/column/20080522/1003240/
# ファイルを任意の大きさに分割するツールです。
#-----------------------------------------------------------------------

#-----------------------------------------------------------------------
# ファイルの分割プログラム
#-----------------------------------------------------------------------
分割識別ヘッダ＝「part-」
分割サイズ＝1024 * 300 # 300KB
# -- 分割におけるメモ
# 分割サイズ＝1024 * 1024 * 1.2 # FD(1.2M)
# 分割サイズ＝1024 * 1024 * 1.4 # FD(1.4M)
# 分割サイズ＝1024 * 1024 * 8 # SDカード(8M)
#-----------------------------------------------------------------------
ファイル選択して分割ファイルに代入。
もし、分割ファイル＝空ならば、終わる。
ファイル名＝分割ファイルからファイル名抽出
出力パス＝「{分割ファイルからパス抽出}分割-{ファイル名}\」
出力パスにフォルダ作成。
#-----------------------------------------------------------------------
# 分割数を計算
バッファサイズ=1024 * 512 # 512KB
B=分割ファイルのファイルサイズ
CNT=INT(B÷分割サイズ)
もし、(B%分割サイズ)<>0ならば、CNT=CNT+1
# 実際の分割
Ｈ＝分割ファイルを「読」でファイルストリーム開く
ファイル一覧＝空。
Iを0から（CNT-1)まで繰り返す
　　# 保存用のファイル
　　F=「{分割識別ヘッダ}{ファイル名}.{I+1}」
　　ファイル一覧にFを配列追加。
　　Hで(I*分割サイズ)にファイルストリーム位置設定。
　　# 巨大ファイル対策 - コピーバッファに分けてコピー
　　今回サイズ＝分割サイズ
　　次回位置＝(I+1)*分割サイズ
　　もし、次回位置＞Bならば、
　　　　今回サイズ＝B-I*分割サイズ
　　H2=出力パス&Fを「書」でファイルストリーム開く。
　　CNT2 = INT(今回サイズ÷バッファサイズ)
　　もし、(今回サイズ%バッファサイズ)<>0ならば、CNT2=CNT2+1
　　Jを0からCNT2まで繰り返す
　　　　SZ=バッファサイズ。
　　　　NEXT=(J+1)*バッファサイズ
　　　　もし、NEXT>今回サイズならば
　　　　　　SZ=今回サイズ-J*バッファサイズ
　　　　S=HでSZをファイルストリーム読む。
　　　　H2でSをファイルストリーム書く。
　　　　母艦＝「{J}/{CNT2} - {F}/{CNT}」
　　H2をファイルストリーム閉じる。
　　Fを表示。
Hをファイルストリーム閉じる。
#-----------------------------------------------------------------------
# 結合用のバッチファイルを保存
結合用ファイル一覧＝ファイル一覧を「+」で配列結合
BAT名=「{出力パス}{ファイル名}.bat」
「rem ファイルの結合用バッチファイル
copy /b {結合用ファイル一覧} {ファイル名}
pause」をBAT名に保存。
#-----------------------------------------------------------------------
# ファイルが壊れてないかをチェックするプログラムを生成
もし、分割サイズ＜(1024*1024*3)ならば
　　結果＝「NG=""{~}」
　　ファイル一覧を反復
　　　　H=出力パス＆対象を開いてMD5取得
　　　　結果＝結果＆「H="{対象}"を開いてMD5取得。」＆改行＆
　　　　　　　「もし(H≠"{H}")ならば、NG=NG&"{対象}"&改行」＆改行。
　　結果＝結果＆「もし,NG=""ならば"OK"と言う。{~}」＆
　　　　「違えば,"ファイルが壊れています:"&改行&NGを言う。{~}終わる。」
　　結果を「{出力パス}ファイルチェック.nako」へ保存。
#-----------------------------------------------------------------------
「完了しました」と言って、終わる。
#----------------------------------------------------------------------- 


