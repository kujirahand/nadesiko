　#-------------------------------------------
　#【ソフト名】　円グラフ
　#【作　　者】　SWinX
　#【連 絡 先】　swinx@plala.to
　#【U  R  L 】　ありません
　#【投 稿 日】　2007/10/8 
　#【ジャンル】　グラフィック
　#【なでしこバージョン】　ver.1.50045
　#【なでしこの実行方法】　標準GUI(vnako.exe)
　#【転　　載】　全て許可
　#【利用作成するファイル】　なし
　#【コメント】　OSC2007の私の宿題です。
　# 　　　　　　 お隣のブースでデモされていたZKに触発されて作りました。
　#------------------------------------------
母艦=「円グラフサンプル」

A=「リンゴ,50
バナナ,20
すいか,30」をCSV取得
仮イメージとはイメージ
その幅=300
その高さ=200
塗色リスト=「{赤色},{黄色},{緑色}」を","で区切ったもの
Aを仮イメージで円グラフ描画

リンゴバーとはバー
その位置は仮イメージの下側
その最小値は0
その最大値は100
その変更した時は
　A[0,1]=リンゴバー
　Aを仮イメージで円グラフ描画

●円グラフ描画(Aを{グループ}OBJで)
　OBJを白色で画面クリア
　線太さ=1。線色=黒色
　塗りスタイル=「透明」
　W=OBJ→W。H=OBJ→H
　OBJの0,0からW,Hへ円
　CX=W/2。CY=H/2
　OBJのCX,CYからCX,0へ線
　全体=Aの1を表列合計
　累計=0。t0=0
　Aを反復
　　累計=累計+対象[1]
　　t=2*PI*累計/全体。tn=(t0+t)/2
　　LX=CX*(1+SIN(t))。LY=CY*(1-COS(t))
　　OBJのCX,CYからLX,LYへ線
　　PX=CX*(1+SIN(tn)/2)。PY=CY*(1-COS(tn)/2)
　　OBJのPX,PYを塗色リスト[回数-1]で黒色まで塗る
　　t0=t
