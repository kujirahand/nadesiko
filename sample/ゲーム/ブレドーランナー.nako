#-------------------------------------------
#【ソフト名】　ブレドーランナー
#【作　　者】　SaRaSa
#【連 絡 先】　kiyota@ma.akari.ne.jp
#【U  R  L 】　http://www.geocities.jp/nihongo_program/
#【投 稿 日】　2005/05/08
#【ジャンル】　ゲーム
#【なでしこバージョン】　ver.1.21
#【なでしこの実行方法】　標準GUI(vnako.exe)
#【転　　載】　全て許可
#【利用作成するファイル】　なし
#【コメント】　時間制限付き迷路脱出ゲーム
#------------------------------------------

横数=20 '必ず偶数
縦数=20 '必ず偶数
幅=16
線太さ=1
文字サイズ=11

母艦のテキストは「ブレドーランナー」
母艦のＷは(横数+1)*幅+8
母艦のＨは(縦数+3)*幅+27
母艦を中央移動

幅/2,幅/2へ移動
「2119年、高層ビルに引きこもりがちな人類を
ある病が蝕んでいた。{\n}
胴体がぶれる病『ブレドー』は、人類を走る
ことすらままならぬ状態にした。{\n}
危機に瀕した人類は、健全な肉体を取り戻す
べく、戦士ブレドーランナーを養成した。{\n}
彼は今、自身の肉体との最終戦争に勝利し、
人類の希望を証明するため、走るのである。
========================================{\n}
▼ ←↑→↓を使い、戦士を操作して下さい
　(動きがぶれるのは、ブレドーの症状){\n}
▼ 制限時間内に赤のゴールへ向って下さい
　(緑を踏むと、時間が少し増えます){\n}
▼ ステージを進むと、時間が短くなります
　(全５ステージで、終了となります){\n}
▼▼▼　何かのキーを押して下さい　▼▼▼」と表示。

色[0]=「$008080」
色[1]=「$FFFFFF」
色[2]=「$00FF00」
色[3]=「$FF0080」
色[4]=「$000000」

絵[0]=「1144411111444111141411111444444111114141144441111411444114111111」
絵[1]=「1144411111444111111411411444444114114111114441111141444111411111」
絵[2]=「1144411111444111111414111444441114141111114444111141141111411411」
絵[3]=「1144411111444111141411111444441111141411144441111411411114114111」

絵を反復
　「絵{回数-1}とはイメージ
　そのＸは(回数-1)*幅
　そのＹは0
　そのＷは幅
　そのＨは幅
　その可視はオフ
　絵{回数+3}とはイメージ
　そのＸは(回数+3)*幅
　そのＹは0
　そのＷは幅
　そのＨは幅
　その可視はオフ
　行を0から7まで繰り返す
　　列を0から7まで繰り返す
　　　線色=色[MID(絵[{回数-1}],8*行+列+1,1)]
　　　塗り色=色[MID(絵[{回数-1}],8*行+列+1,1)]
　　　絵{回数-1}の列*2,行*2から(列+1)*2,(行+1)*2へ四角
　絵{回数-1}を絵{回数+3}の0,0へ画像コピー
　絵{回数+3}を画像水平反転」をナデシコする

人とはアニメ

残秒とはタイマー
残=50

欄とはラベル
そのＸは幅
そのＹは(縦数+1)*幅
そのＷは(横数-1)*幅
そのＨは幅
その文字色は白色
その可視はオフ

方向=「0{\n}1{\n}2{\n}3」
縦量=「0{\n}-1{\n}0{\n}1」
横量=「-1{\n}0{\n}1{\n}0」
面=1
得点=0

母艦のキー押した時は〜主処理
待機

●主処理
　OR(始縦-終縦,始横-終横)>-1でループ
　　始縦=2*(乱数(縦数/2-2)+1)
　　始横=2*(乱数(横数/2-2)+1)
　　終縦=2*(乱数(縦数/2-2)+1)
　　終横=2*(乱数(横数/2-2)+1)

　迷路作成

　縦=始縦
　横=始横
　人のＸは横*幅
　人のＹは縦*幅
　人のＷは幅
　人のＨは幅
　人の値は1000
　人の画像は「絵6{\n}絵7」

　欄の文字色は白色
　欄の可視はオン

　残=50-(面-1)*10
　残秒について
　　値は100
　　時満ちた時は〜
　　　残=小数点切り下げ(残-0.1,1)
　　　欄のテキストは「ステージ：{面}　残り時間：{残}」
　　　もし残<0.1ならば
　　　　残秒の有効はオフ
　　　　1秒待つ
　　　　欄のテキストは「ステージ：{面}　【時間切れ！】」
　　　　1秒待つ
　　　　失敗談
　　開始

　母艦のキー押した時は〜人移動

●迷路作成
　床=「」
　縦を0から縦数まで繰り返す
　　横を0から横数まで繰り返す
　　　もし(縦>=1)かつ(縦<=縦数-1)かつ(横>=1)かつ(横<=横数-1)ならば
　　　　床[縦][横]=0
　　　違えば
　　　　床[縦][横]=1

　数=0
　番=0
　前=-1
　床[始縦][始横]=1
　穴=「」
　穴[数][0]=始縦
　穴[数][1]=始横

　(数>=0)でループ
　　縦=穴[数][0]
　　横=穴[数][1]
　　方向=配列シャッフル(方向)
　　旗=1
　　廻=0
　　(旗>=0)でループ
　　　量縦=縦量[方向[廻]]
　　　量横=横量[方向[廻]]
　　　もし(床[縦+2*量縦][横+2*量横]=0)ならば
　　　　床[縦+1*量縦][横+1*量横]=1
　　　　床[縦+2*量縦][横+2*量横]=1
　　　　もし(縦+2*量縦=終縦)かつ(横+2*量横=終横)ならば
　　　　　床[縦+2*量縦][横+2*量横]=3
　　　　　もし(数>=1)ならば
　　　　　　前=数
　　　　　　数=数-1
　　　　違えば
　　　　　番=番+1
　　　　　穴[番][0]=縦+2*量縦
　　　　　穴[番][1]=横+2*量横
　　　　　数=番
　　　　旗=-1
　　　違えば
　　　　もし(廻=3)ならば
　　　　　もし(前=-1)ならば
　　　　　　床[縦][横]=2
　　　　　違えば
　　　　　　もし(数>前)ならば
　　　　　　　床[縦][横]=2
　　　　　前=数
　　　　　数=数-1
　　　　　旗=-1
　　　廻=廻+1

　黒色で画面クリア
　縦で0から縦数まで繰り返す
　　横で0から横数まで繰り返す
　　　もし(縦=0)また(縦=縦数)また(横=0)また(横=横数)ならば
　　　　床[縦][横]=4
　　　線色=色[床[縦][横]]
　　　塗り色=色[床[縦][横]]
　　　四角(横*幅,縦*幅,(横+1)*幅,(縦+1)*幅)

●人移動
　もし残>0ならば
　　人のＸは横*幅
　　人のＹは縦*幅
　　釦=「{押された仮想キー}」-37
　　もし(釦>=0)かつ(釦<=3)ならば
　　　『人の画像は「絵{釦*2}{\n}絵{釦*2+1}」』をナデシコする
　　　もし(床[縦+縦量[釦]][横+横量[釦]]>=1)ならば
　　　　縦=縦+縦量[釦]
　　　　横=横+横量[釦]
　　　　人のＸは横*幅
　　　　人のＹは縦*幅
　　　　もし(床[縦+縦量[釦]][横+横量[釦]]=2)ならば
　　　　　床[縦+縦量[釦]][横+横量[釦]]=1
　　　　　残=残+5
　　　　もし(床[縦+縦量[釦]][横+横量[釦]]=3)ならば
　　　　　残秒の有効はオフ
　　　　　1秒待つ
　　　　　もし(面<=4)ならば成功談
　　　　　違えば、終幕

●成功談
　得点=得点+残*面*100
　欄の文字色は黒色
　欄のテキストは「人類の獲得筋肉 : {得点} 馬力」
　残=0
　面=面+1
　白色で画面クリア
　文字色=黒色
　人のＸは(横数/2)*幅
　人のＹは7*幅
　0,幅/2へ移動
　「　戦士ブレドーランナーの挑戦は、大成功を
　収め、人類は彼の偉業を賞賛した。{\n}
　彼は新たなる挑戦を誓い、人類は彼と彼の
　肉体に、一筋の希望を見出したのだった。{\n}{\n}{\n}{\n}{\n}
　▼ 第{面}ステージへ進みます
　　　→【ＥＮＴＥＲ】を押して下さい」と表示。
　母艦のキー押した時は〜
　　「{押された仮想キー}」で条件分岐
　　　13ならば主処理

●失敗談
　欄の文字色は黒色
　欄のテキストは「人類の獲得筋肉 : {得点} 馬力」
　白色で画面クリア
　文字色=黒色
　人のＸは(横数/2)*幅
　人のＹは7*幅
　0,幅/2へ移動
　「　かくして、戦士ブレドーランナーの挑戦は
　無残にも失敗に終わった。{\n}
　未来に失望した人類は、さらに引きこもる
　ようになった。{\n}{\n}{\n}{\n}{\n}
　▼ 俺は未来をあきらめないぜ！
　　　→【ＥＮＴＥＲ】を押して下さい{\n}
　▼ 俺も引きこもろう（終）。
　　　→【Ｚ】を押して下さい」と表示。
　母艦のキー押した時は〜
　　「{押された仮想キー}」で条件分岐
　　　13ならば主処理
　　　90ならば終わり

●終幕
　欄の文字色は黒色
　欄のテキストは「人類の獲得筋肉 : {得点} 馬力」
　白色で画面クリア
　文字色=黒色
　人のＸは(横数/2)*幅
　人のＹは6*幅
　0,幅/2へ移動

　「　戦士ブレドーランナーは、全身全霊を懸け
　素晴らしい疾走を人類の前に披露した。{\n}
　人類はその勇姿に胸打たれ、未来に希望を
　取り戻し、筋肉鍛錬に励むようになった。{\n}
　======================================{\n}
　おめでとう、人類！
　ありがとう、ブレドーランナー！{\n}
　人類は彼のことを忘れない……。{\n}
　======================================{\n}{\n}
　▼ 遊んで頂き、大変有難うございました
　　 これにて、終了します
　　　→【何かのキーを押して下さい】」と表示。
　母艦のキー押した時は〜終わり