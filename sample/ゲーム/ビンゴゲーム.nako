　#-------------------------------------------
　#【ソフト名】　ビンゴゲーム
　#【作　　者】　fuji / SWinX
　#【連 絡 先】　swinx@plala.to
　#【U　R　L 】　ありません
　#【投 稿 日】　2005/05/08 
　#【ジャンル】　ゲーム
　#【なでしこバージョン】　ver.1.21
　#【なでしこの実行方法】　標準GUI(vnako.exe)
　#【転　　載】　全て許可
　#【利用作成するファイル】　なし
　#【コメント】　2005/5/7に行われたなでしこオフin横浜にて
　#　　fujiさんが作成なさったビンゴゲームをなでしこに移植しました。
　#　　(fujiさんのオリジナル版はひまわりにて作成されました)
　#　　ボタンの列を可変できるように拡張しました。(変数「ビンゴ数」)
　#　☆遊び方：リセットを押して良さそうなビンゴカードが出来たら準備完了。
　#　　司会者が読み上げた番号のボタンを探して押してください。
　#　　押したボタンは穴を開けたことになり、ボタンが無効になります。
　#　　縦・横・ナナメ(対角線)のどれか一列揃ったらビンゴ！
　#------------------------------------------
番号表とは配列。
!ビンゴ数とは整数=5
//ビンゴ判定に使う文字列を準備
ビンゴ条件とは文字列。それ=空
(ビンゴ数)回
　それ[回数-1]=1
それを「,」で配列結合してビンゴ条件に代入。

判定表とは配列。
ビンゴボタンとは配列。

//ボタンを作り並べる
!ボタンサイズとは整数=50
(ビンゴ数×ビンゴ数)回
　CX=(回数-1)%ビンゴ数。CY=INT((回数-1)/ビンゴ数)
　BX=ボタンサイズ*CX。BY=ボタンサイズ*CY
　ビンゴボタン[回数-1]をボタンとして作成
　ビンゴボタン[回数-1]→X=BX
　ビンゴボタン[回数-1]→Y=BY
　ビンゴボタン[回数-1]→W=ボタンサイズ
　ビンゴボタン[回数-1]→H=ボタンサイズ
　ビンゴボタン[回数-1]→ポケット=「{CX},{CY}」
　ビンゴボタン[回数-1]→クリックした時は〜ボタン処理
　ビンゴボタン[回数-1]→文字サイズ=ボタンサイズ*0.5

番号表作成。
ボタン初期化。
母艦のスタイルは「ツールウィンドウ」
母艦のテキストは「ビンゴ」
「,Mリセット,リセット,,,リセット処理」をメニュー一括作成
母艦のクライアントWはボタンサイズ*ビンゴ数
母艦のクライアントHはボタンサイズ*ビンゴ数

//番号表を作り、シャッフルする
●番号表作成
　(ビンゴ数×ビンゴ数)回
　　番号表[回数-1]=回数を2でゼロ埋め
　番号表を配列シャッフル。

//数字を更新し、有効を元に戻す
●ボタン初期化
　番号表を反復
　　ビンゴボタン[回数-1]→テキスト=対象
　　ビンゴボタン[回数-1]→有効=オン
　　CX=(回数-1)%ビンゴ数。CY=INT((回数-1)/ビンゴ数)
　　判定表[CY][CX]=0

//ボタンが押された時の処理
●ボタン処理
　イベント部品→有効=オフ
　イベント部品→ポケットを「,」で区切る
　判定表[それ\1][それ\0]=1
　//ビンゴ条件達成の判定-横
　判定表を表CSV変換して、ビンゴ条件が何文字目
　もし、それ>0ならば、ビンゴ処理
　//ビンゴ条件達成の判定-縦
　判定表を表行列交換して、表CSV変換して、ビンゴ条件が何文字目
　もし、それ>0ならば、ビンゴ処理
　//ビンゴ条件達成の判定-ななめ
　カウンタAとは整数=0
　カウンタBとは整数=0
　(ビンゴ数)回
　　カウンタA=カウンタA+判定表[回数-1][回数-1]
　　カウンタB=カウンタB+判定表[ビンゴ数-回数][回数-1]
　もし、カウンタA=ビンゴ数ならば、ビンゴ処理
　もし、カウンタB=ビンゴ数ならば、ビンゴ処理

//ビンゴ条件達成時
●ビンゴ処理
　「ビンゴ！」と言う。
　「もう一度遊びますか？」と二択
　もし、それ=はいならば、リセット処理
　違えば、終わり。

//再シャッフルして初期化
●リセット処理
　番号表作成。
　ボタン初期化。
