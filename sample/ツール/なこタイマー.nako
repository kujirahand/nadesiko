#-------------------------------------------
#【ソフト名】　なこタイマー
#【作　　者】　ｍｒ
#【連 絡 先】　http://code.google.com/p/nako-timer/wiki/Renrakucyou
#【U  R  L 】　http://code.google.com/p/nako-timer/
#【投 稿 日】　2009/1/17
#【ジャンル】　ツール
#【なでしこバージョン】　ver.1.5
#【なでしこの実行方法】　標準GUI(vnako.exe)
#【転　　載】　連絡すれば可
#【利用作成するファイル】　なし
#【コメント】　なでしこで作ったキッチンタイマーです。
#------------------------------------------
!母艦設計=「母艦の可視はオフ」

モード＝「UP」  #「UP」or「DOWN」
動作中＝0       #0 or 1
アラーム中＝0   #0 or 1
母艦幅＝130
母艦高＝110
DISP文字サイズ＝25

母艦について
  テキスト＝「Timer」
  幅は母艦幅
  高さは母艦高
  スタイルは「枠なし」
  背景色は$D0D0D0
  ドラッグ移動はオン

母艦を最前面

線色＝黒色
塗り色＝ウィンドウ背景色

線太さ＝1
線色＝$FFFFFF
1,1から母艦幅-2,1へ線
1,1から1,母艦高-2へ線
線色＝$000000
母艦幅-1,0から母艦幅-1,母艦高さへ線
0,母艦高-1から母艦幅,母艦高さ-1へ線
線色＝$888888
母艦幅-2,1から母艦幅-2,母艦高さ-1へ線
1,母艦高-2から母艦幅-1,母艦高さ-2へ線

DISPとはエディタ
その位置は「10,18」
その文字書体は「メイリオ ボールド」
そのテキストは「00:00」
その背景色＝$999999
その文字サイズはDISP文字サイズ
その文字色＝$333333
その編集はオン
その幅は110

分ボタンとは画像ボタン
その位置は「3,67」。その幅は32。その高さは20。
その文字書体は「メイリオ」
その文字サイズは7
そのテキストは「+1min.」
そのフラットはオン
そのクリックした時は
  DISP＝DISPのテキストで分UP

#秒ボタンとは画像ボタン
#その位置は「95,70」。その幅は25
#そのフラットはオン
#その文字書体は「メイリオ ボールド」
#そのクリックした時は
#  DISP＝DISPのテキストで秒UP

開始停止ボタンとは画像ボタン
その位置は「35,70」。その幅は60。その高さは35
そのフラットはオン
その文字書体は「メイリオ ボールド」
そのテキストは「START」
そのクリックした時は
  開始停止処理

ホットキーボタンとは画像ボタン
その位置は「30,5」。その幅は67。その高さは13
そのフラットはオン
その文字書体は「メイリオ」
そのテキストは「HotKey Disable」
その文字サイズは7
そのクリックした時は
その文字色＝$888888
そのヒント＝「Win+S」
そのクリックした時は
  もし、ホットキーボタンのテキスト＝「HotKey Disable」ならば
    「WIN+S」に「開始停止処理」をホットキー登録
    ホットキーボタンのテキストは「HotKey Enable」
    ホットキーボタンの文字色＝$DD0000
  違えば
    「WIN+S」をホットキー解除
    ホットキーボタンのテキストは「HotKey Disable」
    ホットキーボタンの文字色＝$888888

●開始停止処理
  もし、動作中＝オフならば
    動作中＝オン
    リセットボタンの有効＝オフ
    分ボタンの有効＝オフ
    秒ボタンの有効＝オフ
    もし、DISPのテキスト＝「00:00」ならば
      モード＝「UP」
    違えば
      モード＝「DOWN」
    開始停止ボタンのテキストは「STOP」
  違えば
    もし、アラーム中＝オフならば
        動作中＝オフ
        リセットボタンの有効＝オン
        分ボタンの有効＝オン
        秒ボタンの有効＝オン
        開始停止ボタンのテキストは「START」
    違えば
        アラーム中＝オフ
        動作中＝オフ
        リセットボタンの有効＝オン
        分ボタンの有効＝オン
        秒ボタンの有効＝オン
        DISPの文字色＝$333333。DISPの高さは47
        #「時間です」と言う
        開始停止ボタンのテキストは「START」


リセットボタンとは画像ボタン
その位置は「95,67」。その幅は32。その高さは20
そのフラットはオン
その文字書体は「メイリオ」
その文字サイズは7
そのテキストは「Reset  」
そのヒントは「リセット」
そのクリックした時は
  DISP＝「00:00」

終了ボタンとは画像ボタン
その位置は「110,1」。その幅は19。その高さは18
そのフラットはオン
その文字書体は「メイリオ ボールド」
そのテキストは「×」
そのクリックした時は
  おわり

時計とはタイマー
その間隔は、1 /*秒*/
その時満ちた時は
  もし、(アラーム中＝オン)ならば
    もし、DISPの文字色＝$333333ならば
      DISPの文字色＝$888888。DISPの高さは47
    違えば
      DISPの文字色＝$333333。DISPの高さは47
    抜ける
  もし、(動作中＝オフ)ならば
    抜ける
  違えば
    もし、モード＝「UP」ならば
      DISP＝DISPのテキストで秒UP
    違えば
      DISP＝DISPのテキストで秒DOWN
      もし、DISP＝「00:00」ならば
        もし、アラーム中＝オフならば
          アラーム中＝オン
描画
●分UP(Xで)
  F=MID(X,1,2)
  B=MID(X,4,2)
  F=TOSTR(INT(F)+1)を2でゼロ埋め
  もし、F=「100」ならば
    F=「99」
  それ＝「{F}:{B}」

●秒UP(Xで)
  F=MID(X,1,2)
  B=MID(X,4,2)
  B=TOSTR(INT(B)+1)を2でゼロ埋め
  もし、B=「60」ならば
    B=「00」
    F=TOSTR(INT(F)+1)を2でゼロ埋め
  それ＝「{F}:{B}」

●秒DOWN(Xで)
  F=MID(X,1,2)
  B=MID(X,4,2)
  B=TOSTR(INT(B)-1)を2でゼロ埋め
  もし、B=「-01」ならば
    B=「59」
    F=TOSTR(INT(F)-1)を2でゼロ埋め
  それ＝「{F}:{B}」

時計を開始
母艦を中央移動
母艦の可視はオン
DISPの高さは47