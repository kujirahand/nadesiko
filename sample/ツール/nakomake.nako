#------------------------------------------
#【ソフト名】　nakomake
#【 作　者 】　 U D 
#【 U R L  】　http://www.undefin.net/
#【連 絡 先】　  -
#【  転載  】　全て許可
#【なでしこのバージョン】　ver.1.525-1.531
#【 　実行 の 注意点　 】　CNAKO推奨
#【   　　紹　介 　 　 】
# なでしこの実行ファイル作成を支援するコマンドラインツールです。
# vnakoでも動くはずですが、cnakoを推奨します。
# CNAKOで実行ファイルにしてパスを通しておくと、
# 実行ファイル作成を自動化するのに非常に便利です。
# サイトから、ソースと実行ファイルを一緒に入手できます:
#  -> プログラム : http://www.undefin.net/nadesiko/nako/nakomake
# また、詳しい使い方の解説をサイトに書いてあります:
#  -> 使い方     : http://www.undefin.net/nadesiko/xpln/lesson/L1-1
#------------------------------------------
！作者　　　=『 U D 』
！ツール名　=『nakomake』
！バージョン=『0.81』
！関連URL　 =『http://www.undefin.net/nadesiko/xpln/lesson/L1-1』

※ "-runtime"オプションの初期値。
※ 必要ならば書き変えて使って下さい。
ランタイムとは文字列=『C:\Program Files\nadesiko_lang\』

※【なでしこ実行モード】cnako
CNAKOモード=『CON』

#----------------
# <com.nako>

STACKとは配列=コマンドライン
POP()

●POP()
　STACKの0を配列切り取り

●PUSH(E)
　STACKの0にEを配列挿入

●TOP()
　STACK[0]を戻す

# </com.nako>
#----------------

！一時フォルダ　=テンポラリフォルダ&ツール名&`\`
！パックファイル=一時フォルダ&『make.packfile.temp』

暗号化とは整数　　　=不要
実行方式とは文字列　=『vnako』
ターゲットとは文字列
プラグインとは配列
インポートとは配列
出力先とは文字列

TOP()が空でない 間
　対象=POP()
　もし対象が`file`オプションならば
　　POP()にターゲット設定
　違えば、もし対象が`type`オプションならば
　　POP()にタイプ設定
　違えば、もし対象が`plug-ins`オプションならば
　　POP()にプラグイン設定
　違えば、もし対象が`import`オプションならば
　　POP()にインポート設定
　違えば、もし対象が`runtime`オプションならば
　　POP()にランタイム設定
　違えば、もし対象が`output`オプションならば
　　POP()にアウトプット設定
　違えば、もし対象が`encrypt`オプションならば
　　暗号化は必要
　違えば、もし対象がヘルプオプションならば
　　マニュアル表示
　　終わる
　違えば
　　対象にターゲット設定

もしターゲットが存在するんでなければ
　「NO SUCH INPUT FILE:{~}  {ターゲット}」と表示
　終わる

もし出力先が空ならば
　ターゲットからファイル名抽出
　それを空に拡張子変更
　それ&`.exe`にアウトプット設定

※出力先がファイルでなくフォルダなら
もし出力先のフォルダ存在するならば
　出力先=出力先に終端パス追加
　ターゲットからファイル名抽出
　それを空に拡張子変更
　出力先=出力先&それ&`.exe`

もし一時フォルダが存在するならば
　一時フォルダのフォルダ削除
一時フォルダのフォルダ作成

出力先に実行方式でターゲットの実行ファイル作成
プラグインのプラグインファイルコピー

終わる

●実行ファイル作成({文字列}OUTPUTに{文字列="vnako"}TYPEとして{文字列}INPUTの|TYPEでINPUTを)
　# 必要な変数の設定。（関数だけを他で使う場合は、ここを適宜書き換える
　PACKFILEとは文字列=パックファイル
　RUNTIMEとは文字列 =ランタイム
　IMPORTSとは配列　 =インポート
　ENCRYPTとは整数　 =暗号化
　# その他のプログラム用の変数
　パックリストとは配列
　もしTYPEが`vnako`ならば
　　「{RUNTIME}lib\vnako.nako=vnako.nako=0」をパックリストに配列追加
　IMPORTSを反復
　　対象=対象を作業フォルダで相対パス展開
　　もし対象が存在するならば
　　　対象からファイル名抽出
　　　＃対象の作業フォルダを空に置き換え
　　　「{対象}={_}=0」をパックリストに配列追加
　「{INPUT}=nadesiko.nako={ENCRYPT}」をパックリストに配列追加
　パックリストをPACKFILEにパックファイル作成
　「{RUNTIME}{TYPE}.exe」とPACKFILEをOUTPUTにパックファイル結合

●プラグインファイルコピー({配列}PLUGINSの)
　PLUGINSフォルダとは文字列=ランタイム&『plug-ins\』
　移動先フォルダとは文字列=出力先からパス抽出&『plug-ins\』
　もし移動先フォルダが存在するんでなければ
　　移動先フォルダのフォルダ作成
　プラグインを反復
　　もし対象が『.*\.[dD][lL][lL]』に正規表現一致でなければ
　　　対象=対象&『.dll』
　　もしPLUGINSフォルダ&対象が存在するならば
　　　PLUGINSフォルダ&対象を移動先フォルダ&対象にファイルコピー

●パックファイル作成(AをBに)
　「『{A}』を、
『{B}』に、
パックファイル作成。
」と表示
　AをBにシステム:パックファイル作成

●パックファイル結合(AとBをCに)
　「『{A}』と、
『{B}』を、
『{C}』に、
パックファイル結合。
」と表示
　AとBをCにシステム:パックファイル結合

●ファイルコピー(AからBへ|AをBに)
　「『{A}』を、
『{B}』に、
ファイルコピー。
」と表示
　AをBにシステム:ファイルコピー

●フォルダ削除(Sを|Sの|Sから)
　「『{S}』を、
フォルダ削除。
」と表示
　Sをシステム:フォルダ削除

●フォルダ作成(Sに|Sへ|Sの)
　「『{S}』へ、
フォルダ作成。
」と表示
　Sへシステム:フォルダ作成

●ターゲット設定({文字列}TARGETに)
　もしTARGETが存在するならば
　　ターゲット=TARGETを作業フォルダで相対パス展開

●アウトプット設定({文字列}OUTPUTに)
　出力先=OUTPUTを作業フォルダで相対パス展開

●タイプ設定({文字列}TYPEに)
　TYPE=TYPEを小文字変換
　もしTYPEが『[vgc]nako』に正規表現一致するならば
　　実行方式=TYPE

●プラグイン設定({文字列}PLUGINSに)
　ARRとは配列=PLUGINSを『[,;]』で正規表現区切る
　もしARRの要素数が0ならば
　　プラグインにPLUGINSを配列追加
　違えば
　　プラグインにARRを配列一括追加

●インポート設定({文字列}IMPORTSに)
　ARRとは配列=IMPORTSを『[,;]』で正規表現区切る
　もしARRの要素数が0ならば
　　インポートにIMPORTSを配列追加
　違えば
　　インポートにARRを配列一括追加

●ランタイム設定({文字列}RUNTIMEに)
　もしRUNTIMEがフォルダ存在するならば
　　ランタイム=RUNTIMEに終端パス追加

●配列一括追加({参照渡し 配列}AR1に{値渡し 配列}AR2を)
　AR2を反復
　　AR1に対象を配列追加
　AR1を戻す

●オプション({文字列}ARGが{文字列}OPT)
　ARG=ARGを小文字変換
　OPT=`-`&(OPTを小文字変換)
　もしARGがOPTならば
　　はいを戻す
　違えば、もしARGが(OPTの2文字左部分)ならば
　　はいを戻す
　違えば
　　いいえを戻す

●ヘルプオプション({文字列}ARGが)
　もしARGが`?`ならば
　　はいを戻す
　違えば
　　ARGが`help`オプションかを戻す

●マニュアル表示
　「{ツール名} v.{バージョン} -- 使い方
作者:{作者}
-----------------------------------------------------------------------

> nakomake [<INPUT_FILE>] [-<OPTION> <ARG>]...

<INPUT_FILE>は必須だが、オプションはいずれも必須ではない。

*** 引数を取るオプション

-f, -file
  実行ファイルにする対象を指定。<INPUT_FILE>の代わり。
-t, -type
  なでしこの実行方式をvnako/gnako/cnakoのいずれかで指定。
  デフォルトはvnako。
-i, -import
  梱包するファイル一覧を指定。カンマまたはセミコロン区切り。
-p, -plug-ins
  使用するプラグイン一覧を指定。カンマまたはセミコロン区切り。
-r, -runtime
  ランタイムのあるフォルダを指定。
  デフォルトは"C:\Program Files\nadesiko_lang\"。
-o, -output
  実行ファイルの出力先を指定。
  デフォルトは<INPUT_FILE>と同名のEXEファイル。

*** 引数を取らないオプション

-e, -encrypt
  このオプションを使用すると、ソースコードを暗号化する。
  -import で指定した、他の取り込むファイルは暗号化しない。
-h, -help, ?
  このオプションを使用すると、ヘルプを表示して終了する。

-----------------------------------------------------------------------
For more info:
  {関連URL}
」と表示
　戻る

